definition {

	macro addSynonymSet {
		LexiconEntry.gotoAdd();

		Type(
			locator1 = "SearchAdmin#SYNONYMS_MULTI_SELECT_FIELD",
			value1 = "${synonyms}");

		KeyPress(
			locator1 = "SearchAdmin#SYNONYMS_MULTI_SELECT_FIELD",
			value1 = ",");

		for (var synonym : list "${synonyms}") {
			AssertElementPresent(
				key_synonym = "${synonym}",
				locator1 = "SearchAdmin#SYNONYMS_MULTI_SELECT_VALUE");
		}

		Button.clickPublish();
	}

	macro assertEngineVendorAndVersions {
		AssertTextEquals.assertPartialText(
			locator1 = "SearchAdmin#SEARCH_ENGINE_INFORMATION",
			position = "1",
			searchInformation = "${engineVendor}",
			value1 = "${engineVendor}");

		AssertTextEquals.assertPartialText(
			locator1 = "SearchAdmin#SEARCH_ENGINE_INFORMATION",
			position = "2",
			searchInformation = "${clientVersion}",
			value1 = "${clientVersion}");

		AssertTextEquals.assertPartialText(
			locator1 = "SearchAdmin#SEARCH_ENGINE_INFORMATION",
			position = "3",
			searchInformation = "${serverVersion}",
			value1 = "${serverVersion}");
	}

	macro deleteSynonymSet {
		LexiconEntry.delete(rowEntry = "${synonymSet}");

		AssertElementNotPresent(
			key_synonyms = "${synonymSet}",
			locator1 = "SearchAdmin#SYNONYMS_ENTRY");
	}

	macro editSynonymSet {
		LexiconEntry.gotoEntryMenuItem(
			menuItem = "Edit",
			rowEntry = "${synonymSet}");

		if (isSet(deleteSynonym)) {
			Click(
				key_synonym = "${deleteSynonym}",
				locator1 = "SearchAdmin#SYNONYMS_MULTI_SELECT_REMOVE");

			AssertElementNotPresent(
				key_synonym = "${deleteSynonym}",
				locator1 = "SearchAdmin#SYNONYMS_MULTI_SELECT_VALUE");
		}

		if (isSet(addSynonyms)) {
			Type(
				locator1 = "SearchAdmin#SYNONYMS_MULTI_SELECT_FIELD",
				value1 = "${addSynonyms}");

			KeyPress(
				locator1 = "SearchAdmin#SYNONYMS_MULTI_SELECT_FIELD",
				value1 = ",");
		}

		Button.clickPublish();
	}

	macro executeSearchActions {
		Panel.expandPanel(panel = "Index Actions");

		AssertClick.assertTextClickAtAndPause(
			key_actionsDescription = "${actionsDescription}",
			locator1 = "Button#EXECUTE_ACTION",
			value1 = "Execute");

		var pauseCount = "0";

		while ((IsElementPresent(locator1 = "ControlMenu#RELOAD_TOOLTIP")) && (!("${pauseCount}" == "24"))) {
			Pause(locator1 = "5000");

			var pauseCount = MathUtil.sum("${pauseCount}", "1");

			echo("Pause count: ${pauseCount}");
		}

		if ("${pauseCount}" == "24") {
			fail("Reindex timed out after 2 minutes.");
		}
	}

}